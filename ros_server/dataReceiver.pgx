<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="dataReceiver" access="public">
    <Locals>
      <Local name="l_nStartTime" type="num" xsi:type="array" size="1" />
      <Local name="l_nMsgRecvState" type="num" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  // Copyright (c) 2016, Ocado Technology - Robotics Research Team
  //
  // Licensed under the Apache License, Version 2.0 (the "License");
  // you may not use this file except in compliance with the License.
  // You may obtain a copy of the License at
  //
  //     http://www.apache.org/licenses/LICENSE-2.0
  //
  // Unless required by applicable law or agreed to in writing, software
  // distributed under the License is distributed on an "AS IS" BASIS,
  // WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
  // See the License for the specific language governing permissions and
  // limitations under the License.



  // state machine at initial state
  // 0: receive header
  // 1: decode header
  // 2: receive body
  // 3: decode body
  // 4: push motion into buffer
  // 5: send ACK
  l_nMsgRecvState=0

popUpMsg("Data Receiver started...")

  while (true)
    // usually for state machines a switch-case statement is a neat implementation
    // HOWEVER, at every iteration of while() this task is sequenced and only called
    // after whatever ms is was defined when created.
    // As a result, receiving one single message, pushing motion and sending ACK
    // would take 6 * task period using switch-case statement.
    // The desirable behaviour is to receive and decode a full message, push motion
    // into buffer and then send ACK to client WITHIN ONE ITERATION of while(),
    // hence the if statement sequence below
    l_nStartTime = clock()
    if (l_nMsgRecvState==0)
        call recvMsgHeader(l_nMsgRecvState)
    endIf
    nHeaderTime = clock() - l_nStartTime
    
    if (l_nMsgRecvState==1)
      // popUpMsg("State: "+toString("", l_nMsgRecvState))
      call decodeMsgHeader(l_nMsgRecvState)
      // popUpMsg("State after msgHead: "+toString("", l_nMsgRecvState))
    endIf
    if (l_nMsgRecvState==2)
      // popUpMsg("State: "+toString("", l_nMsgRecvState))
      call recvMsgBody(l_nMsgRecvState)
    endIf
    if (l_nMsgRecvState==3)
      // popUpMsg("State: "+toString("", l_nMsgRecvState))
      call decodeMsgBody(l_nMsgRecvState)
    endIf
    if (l_nMsgRecvState==4)
      // popUpMsg("State: "+toString("", l_nMsgRecvState))
        call pushMotion(l_nMsgRecvState)
    endIf
    if (l_nMsgRecvState==5)
      call encodeAck()
      call sendAck(l_nMsgRecvState)
      endIf
    nElapsedTime = clock() - l_nStartTime - nHeaderTime
    
    // assert state machine is working OK
    if (l_nMsgRecvState<0 or l_nMsgRecvState>5)
      popUpMsg("Data reception state machine error")
    endIf

    // sequence task
    delay(0)


  endWhile
end]]></Code>
  </Program>
</Programs>
